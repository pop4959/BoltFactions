plugins {
    id 'java'
    id 'com.modrinth.minotaur' version "2.+"
}

group = 'org.popcraft'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "codemc-repo"
        url = "https://repo.codemc.io/repository/maven-public/"
    }
    maven {
        name = "factions repo"
        url = "https://dependency.download/releases"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.21.8-R0.1-SNAPSHOT"
    compileOnly "org.popcraft:bolt-common:1.0.480"
    compileOnly "org.popcraft:bolt-bukkit:1.0.480"
    compileOnly "dev.kitteh:factions:4.0.0" transitive false
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

modrinth {
    token.set(System.getenv("MODRINTH_TOKEN"))
    projectId.set("boltfactions")
    versionName.set("${project.name} ${version}")
    versionNumber.set("${version}")
    versionType.set("release")
    uploadFile.set(tasks.jar)
    gameVersions.addAll("1.21.8", "1.21.10", "1.21.11")
    loaders.addAll("bukkit", "spigot", "paper")
    dependencies {
        required.project "bolt"
    }
}
